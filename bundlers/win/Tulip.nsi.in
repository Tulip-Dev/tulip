!define CHECK_PYTHON "@TULIP_BUILD_PYTHON_COMPONENTS@"
!define PYTHON_INSTALLER_EXT "@PYTHON_INSTALLER_EXT@"

!include "MUI.nsh"
!include "x64.nsh"
!include "FileAssociation.nsh"

Function .onInit
  ${If} ${RunningX64}
   Goto Yes
  ${Else}
    MessageBox MB_OK "You cannot install the 64 bit version of Tulip @TulipVersion@ on a Windows 32 bit system."
    Abort
  ${Endif}
Yes:
FunctionEnd
  InstallDir "$PROGRAMFILES64\Tulip-@TulipVersion@"
  !if ${CHECK_PYTHON} == "ON"
    Name "Tulip-@TulipVersion@ (x64)"
  !else
    Name "Tulip-@TulipVersion@ (x64 without Python Components)"
  !endif

!define MUI_ICON "files\share\tulip\bitmaps\tulip.ico"
!define MUI_UNICON "files\share\tulip\bitmaps\tulip.ico"

RequestExecutionLevel admin

!define MUI_ABORTWARNING

; Installer pages
!insertmacro MUI_PAGE_LICENSE "COPYING.LESSER"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

; Uninstaller pages
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Language
!insertmacro MUI_LANGUAGE "English"

; Setup file
!ifndef OUT_FILE
!if ${CHECK_PYTHON} == "ON"
!define OUT_FILE "tulip-@TulipVersion@_x64_python@PYTHON_VERSION_NO_DOT@_setup.exe"
!else
!define OUT_FILE "tulip-@TulipVersion@_x64_no_python_setup.exe"
!endif
!endif
OutFile "${OUT_FILE}"

!if ${CHECK_PYTHON} == "ON"

Function CheckAndInstallPython
  SetRegView 64
  ReadRegStr $0 HKCU SOFTWARE\Python\PythonCore\@PYTHON_VERSION@\InstallPath ""
  IfErrors +2 0
  IfFileExists $0\python.exe end 0
  ReadRegStr $0 HKLM SOFTWARE\Python\PythonCore\@PYTHON_VERSION@\InstallPath ""
  IfErrors install 0
  IfFileExists $0\python.exe end install
  install:
        MessageBox MB_OK "Your system does not appear to have Python @PYTHON_VERSION@ (x86-64) installed.$\n$\nIt is now required to run Tulip @TulipVersion@.$\n$\nPress OK to download https://www.python.org/ftp/python/@PYTHON_VERSION_WITH_PATCH@/@PYTHON_INSTALLER@$\n$\nand install Python @PYTHON_VERSION@ (x86-64)."
        inetc::get "https://www.python.org/ftp/python/@PYTHON_VERSION_WITH_PATCH@/@PYTHON_INSTALLER@" "python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@"
!if ${PYTHON_INSTALLER_EXT} == "msi"
        ExecWait '"msiexec" /i "python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@"' $0
!else
	ExecWait '"python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@"' $0
!endif
        Delete python-@PYTHON_VERSION_WITH_PATCH@.@PYTHON_INSTALLER_EXT@
		${If} $0 != 0
		   DetailPrint "MSI return code was $0"
		   MessageBox MB_OK "Python @PYTHON_VERSION@ (x86-64) installation failed. Tulip @TulipVersion@ installation will proceed$\n$\nbut Tulip will not work, until Python @PYTHON_VERSION@ (x86-64) is installed."
		${EndIf}
  end:  
FunctionEnd

!endif

!define SHCNE_ASSOCCHANGED 0x08000000
!define SHCNF_IDLIST 0

Function RefreshShellIcons
  ; By jerome tremblay - april 2003
  System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
  (${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
FunctionEnd

Function un.RefreshShellIcons
  ; By jerome tremblay - april 2003
  System::Call 'shell32.dll::SHChangeNotify(i, i, i, i) v \
  (${SHCNE_ASSOCCHANGED}, ${SHCNF_IDLIST}, 0, 0)'
FunctionEnd

Section "Tulip"
  SetShellVarContext all
  SetOutPath $INSTDIR

!if ${CHECK_PYTHON} == "ON"
  SetRegView 64
  Call CheckAndInstallPython
!endif

File /r files\*.*

  CreateDirectory "$SMPROGRAMS\Tulip-@TulipVersion@ (x64)"
  CreateShortCut "$SMPROGRAMS\Tulip-@TulipVersion@ (x64)\Tulip-@TulipVersion@ agent (x64).lnk" "$INSTDIR\bin\tulip.exe" "" "$INSTDIR\share\tulip\bitmaps\tulip.ico"
  CreateShortCut "$SMPROGRAMS\Tulip-@TulipVersion@ (x64)\Tulip-@TulipVersion@ (x64).lnk" "$INSTDIR\bin\tulip_perspective.exe" "-p Tulip" "$INSTDIR\share\tulip\bitmaps\tulip.ico"
  CreateShortCut "$SMPROGRAMS\Tulip-@TulipVersion@ (x64)\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  CreateShortCut "$DESKTOP\Tulip-@TulipVersion@ agent (x64).lnk" "$INSTDIR\bin\tulip.exe" "" "$INSTDIR\share\tulip\bitmaps\tulip.ico"
  CreateShortCut "$DESKTOP\Tulip-@TulipVersion@ (x64).lnk" "$INSTDIR\bin\tulip_perspective.exe" "-p Tulip" "$INSTDIR\share\tulip\bitmaps\tulip.ico"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip-@TulipVersion@" "DisplayName" "Tulip-@TulipVersion@ (x64)"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip-@TulipVersion@" "UninstallString" "$INSTDIR\Uninstall.exe"
  WriteUninstaller "$INSTDIR\Uninstall.exe"

 ${registerExtension} "$INSTDIR\bin\tulip.exe" ".tlp" "Tulip.File" "Tulip Graph File" "$INSTDIR\share\tulip\bitmaps\tulip-file-icon.ico"
 ${registerExtension} "$INSTDIR\bin\tulip.exe" ".tlpb" "Tulip.BinaryFile" "Tulip Graph Binary File" "$INSTDIR\share\tulip\bitmaps\tulip-file-icon.ico"
 ${registerExtension} "$INSTDIR\bin\tulip.exe" ".tlpz" "Tulip.File.Gzip" "Tulip Graph File (gzipped)" "$INSTDIR\share\tulip\bitmaps\tulip-file-icon.ico"
 ${registerExtension} "$INSTDIR\bin\tulip.exe" ".tlpbz" "Tulip.BinaryFile.Gzip" "Tulip Graph Binary File (gzipped)" "$INSTDIR\share\tulip\bitmaps\tulip-file-icon.ico"
 ${registerExtension} "$INSTDIR\bin\tulip.exe" ".tlpx" "Tulip.ProjectFile" "Tulip Project File" "$INSTDIR\share\tulip\bitmaps\tulip-file-icon.ico"

 Call RefreshShellIcons

SectionEnd

Section "Uninstall"
  SetShellVarContext all
  SetRegView 64
  Delete "$DESKTOP\Tulip-@TulipVersion@ agent (x64).lnk"	
  Delete "$DESKTOP\Tulip-@TulipVersion@ (x64).lnk"	
  RMDir /r "$SMPROGRAMS\Tulip-@TulipVersion@ (x64)"
  RMDir /r "$INSTDIR"
  Delete $INSTDIR\Uninstall.exe
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tulip-@TulipVersion@"

 ${unregisterExtension} ".tlp" "Tulip.File"
 ${unregisterExtension} ".tlpb" "Tulip.BinaryFile"
 ${unregisterExtension} ".tlpz" "Tulip.File.Gzip"
 ${unregisterExtension} ".tlpbz" "Tulip.BinaryFile.Gzip"
 ${unregisterExtension} ".tlpx" "Tulip.ProjectFile"

 Call un.RefreshShellIcons

SectionEnd
